import jaci.gradle.deploy.artifact.FileCollectionArtifact

buildscript {
    ext.kotlin_version = "1.2.30"
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "jaci.openrio.gradle.GradleRIO" version "2018.03.06"
}

apply plugin: "kotlin"

def TEAM = 4330
def ROBOT_CLASS = "frc.team4330.robot.Robot"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", jaci.openrio.gradle.frc.RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', jaci.openrio.gradle.frc.FRCJavaArtifact) {
            targets << "roborio"
        }

        artifact('left_switch', FileCollectionArtifact) {
            files = project.fileTree(dir: 'src/main/paths/left_switch', include: '*.csv')
            directory = '/home/lvuser/paths/left_switch/'
            targets << "roborio"
        }

        artifact('right_switch', FileCollectionArtifact) {
            files = project.fileTree(dir: 'src/main/paths/right_switch', include: '*.csv')
            directory = '/home/lvuser/paths/right_switch/'
            targets << "roborio"
        }

        artifact('center_left', FileCollectionArtifact) {
            files = project.fileTree(dir: 'src/main/paths/center_left', include: '*.csv')
            directory = '/home/lvuser/paths/center_left/'
            targets << "roborio"
        }

        artifact('center_right', FileCollectionArtifact) {
            files = project.fileTree(dir: 'src/main/paths/center_right', include: '*.csv')
            directory = '/home/lvuser/paths/center_right/'
            targets << "roborio"
        }
    }
}

// Kotlin stdlib resides in central
repositories {
    mavenCentral()
}

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
// and NavX.
dependencies {
    compile wpilib()
    compile ctre()
    compile navx()
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile pathfinder()
    compile openrio.powerup.matchData()
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}